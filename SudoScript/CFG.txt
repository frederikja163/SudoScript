Program ::= UnitStatements
UnitStatements ::= UnitStatement newline UnitStatements 
    | UnitStatement
UnitStatement ::= Unit
    | FunctionCall 
    | Rules
    | Givens

Unit ::= unit identifier Parameters leftBrace UnitStatements rightBrace
    | unit identifier leftBrace  UnitStatements rightBrace 
    | unit leftBrace  UnitStatements rightBrace 
Parameters ::= Parameter Parameters 
    | Parameter
Parameter ::= leftParenthesis identifier comma identifier rightParenthesis
    | identifier

FunctionCall ::= identifier Arguments 
    | Union
Arguments ::= Argument space Arguments
    | Argument
Argument ::= Element
    | Cell
Union ::= Cell Union 
    | Cell
Cell ::= leftParenthesis Expression comma Expression rightParenthesis

Expression ::= Expression plus Term
    | Expression minus Term 
    | Term
Term ::= Term multiply Unary
   | Term mod Unary
   | Unary
Unary ::= minus Unary 
   | plus Unary
   | Factor
Factor ::= Element power Factor
    | Element
Element ::= identifier
    | number
    | Range
    | leftParenthesis Expression rightParenthesis
Range ::= leftBracket Expression semicolon Expression rightBracket
    | rightBracket Expression semicolon Expression rightBracket
    | leftBracket Expression semicolon Expression leftBracket
    | rightBracket Expression semicolon Expression leftBracket

Rules ::= rules leftBrace RuleStatements rightBrace
RuleStatements ::= RuleStatement RuleStatements
    | RuleStatement
RuleStatement ::= FunctionCall 
    | Rules

Givens ::= givens leftBrace GivensStatements rightBrace
GivensStatements ::= GivensStatement GivensStatements
    | GivensStatement
GivensStatement ::= Cell space Element 
